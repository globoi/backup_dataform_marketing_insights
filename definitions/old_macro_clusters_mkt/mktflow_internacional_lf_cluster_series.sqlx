config {
  type: 'table',
  tags: ['macrocluster_old','schedule_tag=macro_clusters_mkt_mktflow_internacional_lf_cluster_series'],
  name: 'mktflow_internacional_lf_cluster_series',
  schema: 'macro_clusters_mkt',
  description: '',
  columns: {
  }
,bigquery: {
    labels: {
      mktcloud: "true"
    }
  }
}


--macro cluster lf internacional series
--lf que consumiram series vod ou series na tv ao vivo nos ultimos 180 dias.

with todos_os_generos as (
SELECT distinct  g.name, c.program.title, c.program.id, c.program.subset, c.program.metadata_scheme, c.program.format
FROM  ${ref("conteudo", "all_content_program")} c,
UNNEST(c.genres) g
),

genero_series as (
SELECT *
FROM todos_os_generos
WHERE subset in ('series')
),

consumo_em_genero_series as (
SELECT distinct a.userId as globo_id
FROM ${ref("consumo_enriched", "impression")} a
INNER JOIN genero_series b
ON a.program_id = b.id
WHERE date >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
),

consumo_em_programas_ao_vivo_series as (
SELECT distinct globoid
FROM ${ref("simulcast", "consumo_ibope")} 
WHERE data_consumo >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
AND genero_programa in ('SERIES')
),

uniao_consumo_vod_e_ao_vivo as (
SELECT globo_id, 'consumo series' as base
FROM consumo_em_genero_series
UNION DISTINCT
SELECT globoid as globo_id, 'consumo series' as base
FROM consumo_em_programas_ao_vivo_series)

SELECT distinct 'named_user' as identifier_type, e.globo_id
FROM uniao_consumo_vod_e_ao_vivo d
INNER JOIN ${ref("internacional", "base_lf_internacional")} e
ON d.globo_id = e.globo_id




