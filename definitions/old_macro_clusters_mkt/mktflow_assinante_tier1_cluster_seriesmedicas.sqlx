config {
  type: 'table',
  tags: ['macrocluster_old','schedule_tag=macro_clusters_mkt_mktflow_assinante_tier1_cluster_seriesmedicas'],
  name: 'mktflow_assinante_tier1_cluster_seriesmedicas',
  schema: 'macro_clusters_mkt',
  bigquery: {
    labels: {
      schedule: "semanal",
      tags: "",
      owner: "martech" ,
      horario: "09h00min",
      urban: "list",
      mktcloud: "true"
    }
  },
  description: '',
  columns: {
  }
}


--macro cluster tier 1 series medicas
--assinantes tier 1 que consumiram series medicas vod ou series medicas na tv ao vivo nos ultimos 180 dias.

with todos_os_generos as (
SELECT distinct  g.name, c.program.title, c.program.id, c.program.subset, c.program.metadata_scheme, c.program.format
FROM  ${ref("conteudo", "all_content_program")} c,
UNNEST(c.genres) g
),

genero_series_medicas as (
SELECT *
FROM todos_os_generos
WHERE name in ('Médico')
AND subset in ('series')
),

consumo_em_genero_series_medicas as (
SELECT distinct a.userId as globo_id
FROM ${ref("consumo_enriched", "impression")} a
INNER JOIN genero_series_medicas b
ON a.program_id = b.id
WHERE date >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
),

consumo_em_programas_ao_vivo_series_medicas as (
SELECT distinct globoid
FROM ${ref("simulcast", "consumo_ibope")} 
WHERE data_consumo >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
AND genero_programa in ('SERIES')
AND programa in ('THE GOOD DOCTOR P','CHICAGO MED','UNIDADE BASICA','CHICAGO MED I','HOUSE')
),

uniao_consumo_vod_e_ao_vivo as (
SELECT globo_id, 'consumo series medicas' as base
FROM consumo_em_genero_series_medicas
UNION DISTINCT
SELECT globoid as globo_id, 'consumo series medicas' as base
FROM consumo_em_programas_ao_vivo_series_medicas)

SELECT distinct 'named_user' as identifier_type, e.globo_id
FROM uniao_consumo_vod_e_ao_vivo d
INNER JOIN ${ref("comunicacao", "ultima_comunicacao_geral")} e
ON d.globo_id = e.globo_id
WHERE e.tipo_cadastro = "Assinante"
AND e.mais_canais = false
