config {
  type: 'table',
  tags: ['macrocluster_old','schedule_tag=macro_clusters_mkt_mktflow_assinante_tier2_cluster_musica'],
  name: 'mktflow_assinante_tier2_cluster_musica',
  schema: 'macro_clusters_mkt',
  description: '',
  columns: {
  }
,bigquery: {
    labels: {
      mktcloud: "true"
    }
  }
}

--macro cluster tier 2 musica
--assinantes tier 2 que consumiram programas vod do genero musica, musical ou documentarios musicais, ou que consumiram programas na tv ao vivo do genero ou subgenero musical nos ultimos 180 dias.

with todos_os_generos as (
SELECT distinct  g.name, c.program.title, c.program.id
FROM  ${ref("conteudo", "all_content_program")} c,
UNNEST(c.genres) g
),

genero_musica as (
SELECT *
FROM todos_os_generos
WHERE name in ('Música','Musical','Documentários Musicais')
),

consumo_em_genero_musica as (
SELECT distinct a.userId as globo_id
FROM ${ref("consumo_enriched", "impression")} a
INNER JOIN genero_musica b
ON a.program_id = b.id
WHERE date >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
),

consumo_em_programas_ao_vivo_de_genero_musica as (
SELECT distinct globoid
FROM ${ref("simulcast", "consumo_ibope")} 
WHERE data_consumo >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
AND genero_programa in ('MUSICAL')
),

consumo_em_programas_ao_vivo_de_subgenero_musica as (
SELECT distinct globoid
FROM ${ref("simulcast", "consumo_ibope")}
WHERE data_consumo >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
AND subgenero_programa in ('MUSICAL')
),

uniao_consumo_vod_e_ao_vivo as (
SELECT globo_id, 'consumo musica' as base
FROM consumo_em_genero_musica
UNION DISTINCT
SELECT globoid as globo_id, 'consumo musica' as base
FROM consumo_em_programas_ao_vivo_de_genero_musica
UNION DISTINCT
SELECT globoid as globo_id, 'consumo musica' as base
FROM consumo_em_programas_ao_vivo_de_subgenero_musica)

SELECT 'named_user' as identifier_type, e.globo_id
FROM uniao_consumo_vod_e_ao_vivo d
INNER JOIN ${ref("comunicacao", "ultima_comunicacao_geral")} e
ON d.globo_id = e.globo_id
WHERE e.tipo_cadastro = "Assinante"
AND e.mais_canais = true
