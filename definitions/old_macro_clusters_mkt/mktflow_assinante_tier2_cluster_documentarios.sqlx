config {
  type: 'table',
  tags: ['macrocluster_old','schedule_tag=macro_clusters_mkt_mktflow_assinante_tier2_cluster_documentarios'],
  name: 'mktflow_assinante_tier2_cluster_documentarios',
  schema: 'macro_clusters_mkt',
  description: '',
  columns: {
  }
,bigquery: {
    labels: {
      mktcloud: "true",
      urban: "list"
    }
  }
}

--macro cluster tier 2 documentários
--assinantes tier 2 que consumiram documentários vod ou documentários ao vivo nos ultimos 180 dias.

with todos_os_generos as (
SELECT distinct  g.name, c.program.title, c.program.id, c.program.subset, c.program.metadata_scheme, c.program.format
FROM  ${ref("conteudo", "all_content_program")} c,
UNNEST(c.genres) g
),

genero_documentarios as (
SELECT *
FROM todos_os_generos
WHERE name in ('Documental', 'Documentário') 
),

consumo_em_genero_documentarios as (
SELECT distinct a.userId as globo_id
FROM ${ref("consumo_enriched", "impression")} a
INNER JOIN genero_documentarios b
ON a.program_id = b.id
WHERE date >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
),

consumo_em_programas_ao_vivo_documentarios as (
SELECT distinct globoid
FROM ${ref("simulcast", "consumo_ibope")} 
WHERE data_consumo >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
AND genero_programa in ('DOCUMENTARIO')
),

uniao_consumo_vod_e_ao_vivo as (
SELECT globo_id, 'consumo documentarios' as base
FROM consumo_em_genero_documentarios
UNION DISTINCT
SELECT globoid as globo_id, 'consumo documentarios' as base
FROM consumo_em_programas_ao_vivo_documentarios)

SELECT distinct 'named_user' as identifier_type, e.globo_id
FROM uniao_consumo_vod_e_ao_vivo d
INNER JOIN ${ref("comunicacao", "ultima_comunicacao_geral")} e
ON d.globo_id = e.globo_id
WHERE e.tipo_cadastro = "Assinante"
AND e.mais_canais = true
