config {
    type: 'table',
    tags: ['schedule_tag=cartola_mkt_mktflow_base_automatica_ex_saldo_menos_50_centavos', 'schedule_cron=0 11 * * *'],
    name: 'mktflow_base_automatica_ex_saldo_menos_50_centavos',
    schema: 'cartola_mkt',
    description: '',
    columns: {},

    bigquery: {
        labels: {
            mktcloud: "true",
            schedule: "diario",
            tags: "cartola_mkt"
        }
    }
}

WITH
  fte_full_bonus AS (
  SELECT
    globo_id
  FROM (
    SELECT
      globo_id,
      ledger_entry_bonus_amount,
      ledger_entry_deposit_amount,
      ledger_entry_won_amount
    FROM
      -- `gglobo-caex-analytics-hdg-prd.analytics.f_entry`
      ${ref("analytics", "f_entry")}
    WHERE
      status_id != 1
    QUALIFY
      ROW_NUMBER() OVER(PARTITION BY globo_id ORDER BY bet_datetime ASC) = 1 )
  WHERE
    ledger_entry_bonus_amount > 0
    AND ledger_entry_deposit_amount = 0
    AND ledger_entry_won_amount = 0 ),
  usuarios_com_apenas_uma_entrada AS (
  SELECT
    globo_id,
    COUNT(DISTINCT entry_id) AS qtd_entradas
  FROM
    -- `gglobo-caex-analytics-hdg-prd.analytics.f_entry`
    ${ref("analytics", "f_entry")}
  WHERE
    status_id != 1
  GROUP BY
    globo_id
  HAVING
    qtd_entradas = 1 ),
  saldo_menor_que_50_centavos AS (
  SELECT
    globo_id,
    total_wallet_current_value
  FROM
    -- `gglobo-caex-analytics-hdg-prd.analytics.d_wallet`
    ${ref("analytics", "d_wallet")}
  WHERE
    total_wallet_current_value < 0.5 ),
  depositou AS (
  SELECT
    DISTINCT globo_id
  FROM
    -- `gglobo-caex-analytics-hdg-prd.analytics.f_deposit`
    ${ref("analytics", "f_deposit")}
  WHERE
    status_id = 2 )
SELECT
  f.globo_id,
  total_wallet_current_value AS saldo_atual,
IF
  (d.globo_id IS NOT NULL, TRUE, FALSE) AS realizou_primeiro_deposito
FROM
  fte_full_bonus f
INNER JOIN
  usuarios_com_apenas_uma_entrada
USING
  (globo_id)
INNER JOIN
  saldo_menor_que_50_centavos
USING
  (globo_id)
LEFT JOIN
  depositou d
ON
  f.globo_id = d.globo_id
WHERE
  d.globo_id IS NULL
