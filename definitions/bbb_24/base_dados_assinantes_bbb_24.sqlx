config {
  type: "table",
  schema: 'bbb_24',
  bigquery: {
    labels: {
      schedule: "diario",
      tags: "bbb_24",
      owner: "martech",
      horario: "09h00min"
    }
  },
  description: 'Tabela com os assinantes da base ativa incremental a partir do dia 08/01/2024 e histórico de consumo/cancelamento em BBB.',
  columns: {
    globo_id: 'Código que identifica o usuário.',
    assets: {
      description: "Array com o asset id e a última data em que o assinante apareceu na base ativa pagante com trial.",
      columns: {
        id_asset: 'Código que identifica a assinatura.',
        base_ativa: 'Última data em que o assinante apareceu na base ativa pagante com trial.',
      }
    },
    dt_assinatura_comeco: 'Data início da assinatura do usuário.',
    consumiu_bbb_20: 'Flag se o assinante consumiu o BBB 2020.',
    assinou_cancelou_bbb_20: 'Flag se o usuário assinou entre 01/01/2020 e 30/04/2020, solicitou o cancelamento entre 01/04/2020 e 31/05/2020 e teve a assinatura cancelada no ano de 2020.',
    consumiu_bbb_21: 'Flag se o assinante consumiu o BBB 2021.',
    assinou_cancelou_bbb_21: 'Flag se o usuário assinou entre 01/01/2021 e 30/04/2021, solicitou o cancelamento entre 01/04/2021 e 31/05/2021 e teve a assinatura cancelada no ano de 2021.',
    consumiu_bbb_22: 'Flag se o assinante consumiu o BBB 2022.',
    assinou_cancelou_bbb_22: 'Flag se o usuário assinou entre 01/01/2022 e 30/04/2022, solicitou o cancelamento entre 01/04/2022 e 31/05/2022 e teve a assinatura cancelada no ano de 2022.',
    consumiu_bbb_23: 'Flag se o assinante consumiu o BBB 2023.',
    assinou_cancelou_bbb_23: 'Flag se o usuário assinou entre 01/01/2023 e 30/04/2023, solicitou o cancelamento entre 01/04/2023 e 31/05/2023 e teve a assinatura cancelada no ano de 2023.',
    assinou_2024: 'Flag se o usuário assinou após 08/01/2024.',
    sol_cancel_vol_24: 'Flag se o usuário solicitou o cancelamento após 08/01/2024.'
  }
}

WITH base_bbb_20 AS (
  SELECT
    DISTINCT globo_id,
    TRUE AS consumiu_bbb_20
  FROM
    ${ref('bbb_20', 'status_Erick_historico')}
),
base_bbb_21 AS (
  SELECT
    DISTINCT globo_id,
    TRUE AS consumiu_bbb_21
  FROM
    ${ref('bbb_21', 'raiox_bbb_21_propensao_churn')}
),
base_bbb_22 AS (
  SELECT
    DISTINCT globo_id,
    TRUE AS consumiu_bbb_22
  FROM
    ${ref('bbb_22', 'Raiox_BBB_22_Agrupado_historico')}
),
base_bbb_23 AS (
  SELECT
    DISTINCT globo_id,
    TRUE AS consumiu_bbb_23
  FROM
    ${ref('bbb_23', 'Raiox_BBB_23_Assinantes_Agrupado')}
),
assinantes AS (
  SELECT
    DISTINCT globo_id,
    id_asset,
    MIN(dt_assinatura_comeco) OVER(PARTITION BY globo_id) AS dt_assinatura_comeco,
    MAX(dt_proc) OVER(PARTITION BY id_asset, globo_id) AS base_ativa
  FROM
    ${ref('usuarios', 'base_ativa_incremental_new')}
  WHERE
    dt_proc >= '2024-01-08'
    AND tipo_assinante != 'trial'
    AND assinatura_gplay = 'nacional'
),
assinantes2 AS (
  SELECT
    globo_id,
    id_asset,
    base_ativa,
    dt_assinatura_comeco,
    ROW_NUMBER() OVER(
      PARTITION BY globo_id,
      id_asset,
      base_ativa,
      dt_assinatura_comeco
      ORDER BY
        globo_id,
        id_asset,
        base_ativa,
        dt_assinatura_comeco
    ) AS rm,
  FROM
    assinantes QUALIFY rm = 1
),
base_rein AS (
  SELECT
    DISTINCT id,
    TIMESTAMP(subscription_at) AS data_hora_compra,
    product_name,
    (
      subscription_at BETWEEN '2020-01-01'
      AND '2020-04-30'
    )
    AND (
      unsubscribe_at BETWEEN '2020-04-01'
      AND '2020-05-31'
    )
    AND (
      EXTRACT(
        YEAR
        FROM
          end_cycle_at
      ) = 2020
    ) AS assinou_cancelou_bbb_20,
    (
      subscription_at BETWEEN '2021-01-01'
      AND '2021-04-30'
    )
    AND (
      unsubscribe_at BETWEEN '2021-04-01'
      AND '2021-05-31'
    )
    AND (
      EXTRACT(
        YEAR
        FROM
          end_cycle_at
      ) = 2021
    ) AS assinou_cancelou_bbb_21,
    (
      subscription_at BETWEEN '2022-01-01'
      AND '2022-04-30'
    )
    AND (
      unsubscribe_at BETWEEN '2022-04-01'
      AND '2022-05-31'
    )
    AND (
      EXTRACT(
        YEAR
        FROM
          end_cycle_at
      ) = 2022
    ) AS assinou_cancelou_bbb_22,
    (
      subscription_at BETWEEN '2023-01-01'
      AND '2023-04-30'
    )
    AND (
      unsubscribe_at BETWEEN '2023-04-01'
      AND '2023-05-31'
    )
    AND (
      EXTRACT(
        YEAR
        FROM
          end_cycle_at
      ) = 2023
    ) AS assinou_cancelou_bbb_23,
    case
      when subscription_at >= '2024-01-05' then true
    end as assinou_2024,
    CASE
      WHEN unsubscribe_at >= '2024-01-08' THEN TRUE
    END AS sol_cancel_vol_24,
    subscription_status,
    unsubscribe_at,
    scheduled_unsubscribe_at,
    end_cycle_at
  FROM
    ${ref('sales_gold', 'subscription_details')}
),
base_rein2 AS (
  SELECT
    *
  FROM
    base_rein
    JOIN ${ref('sales_gold', 'last_subscription_state')} AS s ON s.object_id = base_rein.id
  WHERE
    LOWER(product_name) LIKE '%globoplay%'
),
base_rein3 AS (
  SELECT
    globo_id,
    COALESCE(MAX(assinou_cancelou_bbb_20), FALSE) AS assinou_cancelou_bbb_20,
    COALESCE(MAX(assinou_cancelou_bbb_21), FALSE) AS assinou_cancelou_bbb_21,
    COALESCE(MAX(assinou_cancelou_bbb_22), FALSE) AS assinou_cancelou_bbb_22,
    COALESCE(MAX(assinou_cancelou_bbb_23), FALSE) AS assinou_cancelou_bbb_23,
    coalesce(max(assinou_2024), false) as assinou_2024,
    COALESCE(MAX(sol_cancel_vol_24), FALSE) AS sol_cancel_vol_24
  FROM
    base_rein2
  GROUP BY
    1
)
SELECT
  globo_id,
  ARRAY_AGG(STRUCT(id_asset, base_ativa)) AS assets,
  dt_assinatura_comeco,
  IFNULL(consumiu_bbb_20, FALSE) AS consumiu_bbb_20,
  IFNULL(assinou_cancelou_bbb_20, FALSE) AS assinou_cancelou_bbb_20,
  IFNULL(consumiu_bbb_21, FALSE) AS consumiu_bbb_21,
  IFNULL(assinou_cancelou_bbb_21, FALSE) AS assinou_cancelou_bbb_21,
  IFNULL(consumiu_bbb_22, FALSE) AS consumiu_bbb_22,
  IFNULL(assinou_cancelou_bbb_20, FALSE) AS assinou_cancelou_bbb_22,
  IFNULL(consumiu_bbb_23, FALSE) AS consumiu_bbb_23,
  IFNULL(assinou_cancelou_bbb_23, FALSE) AS assinou_cancelou_bbb_23,
  ifnull(assinou_2024, false) as assinou_2024,
  IFNULL(sol_cancel_vol_24, FALSE) AS sol_cancel_vol_24
FROM
  assinantes2
  LEFT JOIN base_bbb_20 USING (globo_id)
  LEFT JOIN base_bbb_21 USING (globo_id)
  LEFT JOIN base_bbb_22 USING (globo_id)
  LEFT JOIN base_bbb_23 USING (globo_id)
  LEFT JOIN base_rein3 USING (globo_id)
GROUP BY
  1,
  3,
  4,
  5,
  6,
  7,
  8,
  9,
  10,
  11,
  12,
  13
