config {
  type: 'table',
  tags: [
    'jornada_consumo',
    'schedule_tag=macro_clusters_mkt_mktflow_assinante_premium_cluster_seriesmedicas',
    "schedule_cron=00 11 * * 1"
    ],
  name: 'mktflow_assinante_premium_cluster_seriesmedicas',
  schema: 'macro_clusters_mkt',
  bigquery: {
    labels: {
      tags: "",
      owner: "martech" ,
      urban: "list",
      mktcloud: "true"
    }
  },
  description: 'assinantes premium que consumiram series medicas vod ou series medicas na tv ao vivo nos ultimos 180 dias.',
  columns: {
  },
}

--macro cluster premium series medicas
--assinantes premium que consumiram series medicas vod ou series medicas na tv ao vivo nos ultimos 180 dias.

with consumo_em_genero_series_medicas as (
  SELECT distinct a.userId as globo_id, sum(hours_watched) as hrs_consumidas
  FROM ${ref('consumo_enriched','impression')} a
  INNER JOIN ${ref('conteudo','acervo')} as b
    ON a.program_id = b.id
  WHERE date >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
    AND (lower(Subgenero_2_Tratado) = 'médico' or lower(Subgenero_2) = 'médico')
  GROUP BY ALL
  HAVING hrs_consumidas >= 0.5 
),

consumo_em_programas_ao_vivo_series_medicas as (
  SELECT distinct globoid, sum(playtime_hours) as hrs_consumidas
  FROM ${ref('simulcast','consumo_ibope')} 
  WHERE data_consumo >= DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 180 DAY)
    AND genero_programa in ('SERIES')
    AND programa in ('THE GOOD DOCTOR P','CHICAGO MED','UNIDADE BASICA','CHICAGO MED I','HOUSE', 'MT CHICAGO MED', 'O SUS NA PANDEMIA', '	MT THE GOOD DOCTOR', 'THE GOOD DOCTOR', 'THE GOOD DOCTOR I')
  GROUP BY ALL
  HAVING hrs_consumidas >= 0.5 
),

uniao_consumo_vod_e_ao_vivo as (
  SELECT globo_id, 'consumo series medicas' as base FROM consumo_em_genero_series_medicas
  
  UNION DISTINCT
  
  SELECT globoid as globo_id, 'consumo series medicas' as base FROM consumo_em_programas_ao_vivo_series_medicas
)

SELECT distinct 'named_user' as identifier_type, e.globo_id
FROM uniao_consumo_vod_e_ao_vivo d
INNER JOIN ${ref('comunicacao','ultima_comunicacao_geral')} e
  ON d.globo_id = e.globo_id
WHERE e.tipo_cadastro = "Assinante"
  AND e.mais_canais = true
