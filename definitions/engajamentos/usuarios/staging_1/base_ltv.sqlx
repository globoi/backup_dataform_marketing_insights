config {
  type: "table",
  name: "base_ltv",
  schema: "usuarios",
    description: 'Base com usuários geraram lucro.',
    columns: {
  AssetId:'Código do item da Cesta (Produto que o cliente adquiriu).',
  globo_id:'Código de identificação do usuário.',
  AssetName:'Nome do produto na cesta.',
  frequencia_cobranca:'Identifica a periodicidade da cobrança do produto adquirido. Ex: Anual, bianual ou mensal.', 
  caracteristica_produto:'Identifica se o produto foi comprado por meio de combo ou não.',
  tipo_de_aquisicao:'Identifica o tipo de movimentação do produto relacionado a compra. (Ex: Cross, Upgrade, Downgrade, etc.).',
  tipo_de_pedido:'Informa o tipo de pedido realizado pelo cliente.(Ex: Produto, Oferta ou Combo).',
  status_calculado:'status da assinatura.',
  visivel_cesta:'Composto com o id do item da cesta do cliente. Mostra somente na linha referente ao que ele efetivamente paga ou contrata.',
  preco:'Informa o preço do Ativo.',
  data_compra:'Data da compra do produto.',
  data_fim_ref:'Identifica a data final de um cliente dentro da base (data de cancelamento, suspensão ou data atual, caso o intem ainda esteja ativo.).',
  origem_id:'Código que identifica a origem do pedido.',
  descricao_origem_id:'Descrição da data origem do pedido.',
  lt:'Identifica o periodo em que o usuário esteve dentro da base, descontando o periódo.',
  lt_sem_desconto:'Identifica o periodo em que o usuário esteve dentro da base sem desconto.',
  lt_mes_cheio:'Identifica o periodo em que o usuário esteve dentro da base dentro de 30 dias.',
  lt_meses:'Identifica o periodo em que o usuário teve dentro da base em contagem de meses.',
  ciclos_pagos:'Identifica a quantidadae de faturas pagas.',
  receita_total:'Identifica o quanto de receita o usuário gerou com uma assinatura.',
  grupo:'Define qual grupo o usuário pertence por meio de seus gastos.',
  tier_fidelidade:'Define qual tier o usuário está de acordo com o grupo especificado.'

}}

WITH step_1 AS (
  SELECT
    globo_id,
    AssetId,
    AssetName,
    status,
    status_calculado,
    enhanced_status,
    data_compra,
    dt_cancelamento_final_ciclo,
    DATE(data_da_suspensao) AS data_da_suspensao,
    ARRAY(
      SELECT
        x
      FROM
        UNNEST(lista_suspensoes) AS x
      ORDER BY
        x
    ) AS lista_suspensoes,
    ARRAY(
      SELECT
        x
      FROM
        UNNEST(lista_reativacoes) AS x
      ORDER BY
        x
    ) AS lista_reativacoes,
    ultima_suspensao,
    ultima_reativacao,
    visivel_cesta,
    origem_id,
    descricao_origem_id,
    tipo_cancelamento,
    caracteristica_produto,
    ProductCode,
    tipo_de_pedido,
    canal_compra,
    forma_pagamento,
    tipo_de_aquisicao,
    frequencia_cobranca,
    ProductName,
    gender,
    age,
    address_city,
    address_state,
    comunica_globo,
    preco,
    ARRAY_REVERSE(lista_suspensoes) [SAFE_OFFSET(0)] AS teste_ultima_susp,
    lista_suspensoes [SAFE_OFFSET(0)] AS teste_primeira_susp,
    ARRAY_LENGTH(lista_suspensoes) AS qtd_susp,
    DATE_DIFF(
      lista_suspensoes [SAFE_OFFSET(0)],
      data_compra,
      DAY
    ) AS dist_primeira_susp_compra
  FROM
     ${ref('sales_enriched','asset_v2')}
  WHERE
    1 = 1
    AND data_hora_compra >= '2018-01-01'
    AND REGEXP_CONTAINS(AssetName, '(?i)(globoplay)')
),
step_2 AS (
  SELECT
    *,
    CASE
      WHEN ARRAY_LENGTH(lista_reativacoes) < ARRAY_LENGTH(lista_suspensoes) THEN ARRAY(
        SELECT
          DATE_DIFF(
            lista_reativacoes [SAFE_OFFSET(y)],
            lista_suspensoes [SAFE_OFFSET(y)],
            DAY
          )
        FROM
          UNNEST(lista_reativacoes) AS x WITH OFFSET AS y
        ORDER BY
          y
      )
      ELSE ARRAY(
        SELECT
          DATE_DIFF(
            lista_reativacoes [SAFE_OFFSET(y)],
            lista_suspensoes [SAFE_OFFSET(y)],
            DAY
          )
        FROM
          UNNEST(lista_suspensoes) AS x WITH OFFSET AS y
        ORDER BY
          y
      )
    END AS duracao_suspensoes
  FROM
    step_1
),
step_3 AS (
  SELECT
    *,
    (
      SELECT
        MAX(x)
      FROM
        UNNEST(duracao_suspensoes) AS x
    ) AS max_dias_suspenso,
    (
      SELECT
        SUM(x)
      FROM
        UNNEST(duracao_suspensoes) AS x
    ) AS dias_suspenso,
    ARRAY(
      SELECT
        x
      FROM
        UNNEST(
          [data_da_suspensao, dt_cancelamento_final_ciclo, CURRENT_DATE()]
        ) AS x
      WHERE
        x IS NOT NULL
      ORDER BY
        x ASC
    ) [SAFE_OFFSET(0)] AS data_fim_ref,
    CURRENT_DATE() AS dt_processamento
  FROM
    step_2
),
step_4 AS (
  SELECT
    *,
    DATE_DIFF(data_fim_ref, data_compra, DAY) AS lt_sem_desconto,
    DATE_DIFF(data_fim_ref, data_compra, DAY) - COALESCE(dias_suspenso, 0) AS lt,
    DATE_DIFF(data_fim_ref, data_compra, MONTH) lt_meses
  FROM
    step_3
),
step_5 AS (
  SELECT
    *,
    FLOOR(DIV(lt, 30)) AS lt_mes_cheio,
    CEIL(
      SAFE_DIVIDE(
        lt,
        (
          CASE
            WHEN LOWER(frequencia_cobranca) = 'mensal' THEN 30
            WHEN LOWER(frequencia_cobranca) = 'anual' THEN 365
            WHEN LOWER(frequencia_cobranca) = 'bianual' THEN 730
            ELSE 30
          END
        )
      )
    ) AS ciclos_pagos
  FROM
    step_4
),
step_6 AS (
  SELECT
    origem_id,
    globo_id,
    AssetId,
    AssetName,
    status,
    status_calculado,
    enhanced_status,
    data_compra,
    dt_cancelamento_final_ciclo,
    data_da_suspensao,
    lista_suspensoes,
    lista_reativacoes,
    ultima_suspensao,
    ultima_reativacao,
    visivel_cesta,
    descricao_origem_id,
    tipo_cancelamento,
    caracteristica_produto,
    ProductCode,
    tipo_de_pedido,
    canal_compra,
    forma_pagamento,
    tipo_de_aquisicao,
    frequencia_cobranca,
    ProductName,
    gender,
    age,
    address_city,
    address_state,
    comunica_globo,
    preco,
    teste_ultima_susp,
    teste_primeira_susp,
    qtd_susp,
    dist_primeira_susp_compra,
    duracao_suspensoes,
    dias_suspenso,
    max_dias_suspenso,
    data_fim_ref,
    dt_processamento,
    lt_sem_desconto,
    lt,
    lt_mes_cheio,
    lt_meses,
    ciclos_pagos,
    ROUND(ciclos_pagos * preco, 2) AS receita_total
  FROM
    step_5
  WHERE
    1 = 1
),

-- oids AS (
--   SELECT
--     EVA_ORIGEM_ID__c as origin_id,
--     EVA_DESCRICAO_TXT__c  as origin_name
--   FROM ${ref('salesforce','EVA_Origem_ID__c_*')}
--   where _TABLE_SUFFIX = FORMAT_DATE('%Y%m%d', DATE_SUB(CURRENT_DATE('America/Sao_Paulo'), INTERVAL 1 DAY))
-- ),

oids2 AS (
  SELECT DISTINCT 
    origin_id,
    origin_name
  FROM
    ${ref('sales_gold','subscription_details')}
),

base AS (
  SELECT
    AssetId,
    globo_id,
    AssetName,
    frequencia_cobranca,
    canal_compra,
    caracteristica_produto,
    tipo_de_aquisicao,
    tipo_de_pedido,
    status_calculado,
    visivel_cesta,
    preco,
    data_compra,
    data_fim_ref,
    origem_id,
    oids2.origin_name AS descricao_origem_id,
    lt,
    lt_sem_desconto,
    lt_mes_cheio,
    lt_meses,
    ciclos_pagos,
    receita_total,
    NTILE(100) OVER (
      ORDER BY
        receita_total DESC
    ) grupo
  FROM
    step_6
  LEFT JOIN
    oids2 on origem_id = oids2.origin_id

  WHERE 1=1
    AND receita_total >= 0
    AND NOT (
      AssetName = 'Globoplay'
      AND frequencia_cobranca = 'Anual'
      AND preco > 500
    )
    AND NOT (
      AssetName = 'Globoplay'
      AND frequencia_cobranca = 'Mensal'
      AND preco > 25
    )
    AND NOT (
      AssetName != 'Globoplay'
      AND frequencia_cobranca = 'Mensal'
      AND preco > 100
    )
    AND NOT (
      frequencia_cobranca IS NULL
      AND preco > 500
    )
),
tier AS (
  SELECT
    *,
    CASE
      WHEN grupo <= 3 THEN 7
      WHEN grupo <= 11 THEN 6
      WHEN grupo <= 25 THEN 5
      WHEN grupo <= 41 THEN 4
      WHEN grupo <= 59 THEN 3
      WHEN grupo <= 79 THEN 2
      ELSE 1
    END AS tier_fidelidade,
    RANGE_BUCKET(receita_total, [120.,350,450,650,1000,1400]) + 1 bucket
  FROM
    base
)
SELECT
  *
FROM
  tier
WHERE
  1 = 1