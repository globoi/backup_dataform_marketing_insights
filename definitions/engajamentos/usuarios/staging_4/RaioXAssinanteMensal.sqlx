config {
    type: "table",
    name: "RaioXAssinanteMensal",
    schema: "usuarios",
    tags: ["usuarios_staging_4",
        "schedule_tag=usuarios_raioxassinantemensal"
    ],

    bigquery: {
        labels: {
            schedule: "diario",
            consumo: "mensal",
            tags: "usuarios",
            owner: "mins",
            horario: "11h00min"
        }
    },
    description: 'Tabela mensal com o consumo e clusters de engajamento dos assinantes ativos desde 2021.',
    columns: {
        globo_id: 'Código de identificação do usuário.',
        mes: 'Mês que o usuário consumiu algum conteúdo.',
        Cluster_Engajamento: 'Cluster de engajamento antigo, utilizado até 09/24. Onde dividíamos a base em quartis, particionado pela semana e ordenado pela quantidade total de horas na semana. Ou seja, quartil 1 = 4. Heavy Users, quartil 2 = 3. Mid-Heavy Users, quartil 3 = 2. Mid-Light Users e quartil 4 = 1. Light Users. E no mensal, consideramos a quantidade de vezes que o usuários fez parte de um cluster, ou seja, se em 3 semanas ele foi heavy user e em 1 semana ele foi light user, ele é considerado heavy user naquele determinado mês da semana de referência.',
        novo_cluster_engajamento: 'Novo cluster de engajamento base no total de horas consumidas na semana. Exemplo: Se o usuário consumiu mais de 10 horas na semana, ele é considerado 4. Heavy Users, se consumiu entre 0 e 1 hora, ele é considerado 1. Light Users, se consumiu entre 1 e 5 horas, ele é considerado 2. Mid-Light Users, se consumiu entre 5 e 10 horas é considerado 3. Mid-Heavy Users e se não estiver em nenhum range desse, é considerado Sem consumo suficiente. E no mensal, consideramos a quantidade de vezes que o usuários fez parte de um cluster, ou seja, se em 3 semanas ele foi heavy user e em 1 semana ele foi light user, ele é considerado heavy user naquele determinado mês da semana de referência.',
        assetid: 'Código que identifica a assinatura.',
        data_hora_compra: 'Identifica a data e hora da compra da assinatura.',
        frequencia_mensal: 'Quantidade de dias no mês que o usuário consumiu algum conteúdo.',
        Ao_Vivo_Tv_Globo_horas_Mensal: 'Quantidade de horas assistidas no mês do ao vivo Globo TVG (program_id 9095).',
        Ao_Vivo_Tv_Globo_qtnd_programas_Mensal: 'Quantidade de programas assistidos no mês do ao vivo Globo TVG (program_id 9095). Nesse caso o valor é 0 ou 1, pois só existe 1 program_id.',
        Ao_Vivo_Tv_Globo_freq_Mensal: 'Quantidade de dias no mês que o usuário consumiu o ao vivo Globo TVG (program_id 9095).',
        Ao_Vivo_Tv_Globo_qtnd_midias_Mensal: 'Quantidade de mídias assistidas no mês do ao vivo Globo TVG (program_id 9095). Alguma mídia atrelada a esse program_id.',
        Filmes_horas_Mensal: 'Quantidade de horas de filmes assistidos no mês. Exemplo: Conteúdos do Telecine, Megapix, Universal, Studio Universal, USA e programas que contenham os subsets filmes e movies.',
        Filmes_qtnd_programas_Mensal: 'Quantidade de filmes assistidos no mês.',
        Filmes_freq_Mensal: 'Quantidades de dias que o usuário assistiu algum filme no mês.',
        Filmes_qtnd_midias_Mensal: 'Quantidade de filmes assistidos no mês.',
        Series_horas_Mensal: 'Quantidade de horas de novelas assistidas no mês.',
        Series_qtnd_programas_Mensal: 'Quantidade de novelas assistidas no mês.',
        Series_freq_Mensal: 'Quantidades de dias que o usuário assistiu alguma novela no mês.',
        Series_qtnd_midias_Mensal: 'Quantidade de mídias distintas de novelas assistidas no mês.',
        Novelas_horas_Mensal: 'Quantidade de horas de séries assistidas no mês.',
        Novelas_qtnd_programas_Mensal: 'Quantidade de séries assistidas no mês.',
        Novelas_freq_Mensal: 'Quantidades de dias que o usuário assistiu alguma série no mês.',
        Novelas_qtnd_midias_Mensal: 'Quantidade de mídias distintas de séries assistidas no mês.',
        Esportes_horas_Mensal: 'Quantidade de horas de conteúdos esportivos assistidos no mês. Exemplo: Conteúdos do Combate, Premiere, Sportv, GE ou que tenham o subset sports atrelado.',
        Esportes_qtnd_programas_Mensal: 'Quantidade de programas de conteúdos esportivos assistidos no mês.',
        Esportes_freq_Mensal: 'Quantidades de dias que o usuário assistiu algum conteúdo esportivo no mês.',
        Esportes_qtnd_midias_Mensal: 'Quantidade de mídias distintas de conteúdos esportivos assistidos no mês.',
        Variedades_horas_Mensal: 'Quantidade de horas de conteúdos de variedades assistidos no mês. Exemplo: Conteúdos do Receitas, Multishow, Viva (que não contenha o subset novela) e programas com o subset varieties.',
        Variedades_qtnd_programas_Mensal: 'Quantidade de programas de conteúdos de variedades assistidos no mês.',
        Variedades_freq_Mensal: 'Quantidades de dias que o usuário assistiu algum conteúdo de variedades no mês.',
        Variedades_qtnd_midias_Mensal: 'Quantidade de mídias distintas de conteúdos de variedades assistidos no mês.',
        Jornalismo_horas_Mensal: 'Quantidade de horas de conteúdos jornalísticos assistidos no mês. Exemplo: Conteúdos GloboNews, G1 ou programas que contenham o subset news.',
        Jornalismo_qtnd_programas_Mensal: 'Quantidade de programas de conteúdos jornalísticos assistidos no mês.',
        Jornalismo_freq_Mensal: 'Quantidades de dias que o usuário assistiu algum conteúdo jornalístico no mês.',
        Jornalismo_qtnd_midias_Mensal: 'Quantidade de mídias distintas de conteúdos jornalísticos assistidos no mês.',
        Reality_horas_Mensal: 'Quantidade de horas de realities assistidos no mês. Exemplo: Programas que contenham o subset realities.',
        Reality_qtnd_programas_Mensal: 'Quantidade de programas de realities assistidos no mês.',
        Reality_freq_Mensal: 'Quantidades de dias que o usuário assistiu algum conteúdo de reality no mês.',
        Reality_qtnd_midias_Mensal: 'Quantidade de mídias distintas de realities assistidos no mês.',
        Outros_Conteudos_horas_Mensal: 'Quantidade de horas de outros conteúdos assistidos no mês. Exemplo: Todos os outros conteúdos que não se enquadram nos clusters anteriores.',
        Outros_Conteudos_qtnd_programas_Mensal: 'Quantidade de programas de outros conteúdos assistidos no mês.',
        Outros_Conteudos_freq_Mensal: 'Quantidades de dias que o usuário assistiu algum programa de outros conteúdos no mês.',
        Outros_Conteudos_qtnd_midias_Mensal: 'Quantidade de mídias distintas de outros conteúdos assistidos no mês.',
        total_horas_Mes: 'Total de horas assistidas pelo usuário no mês.',
        horas_dia_de_semana_mensal: 'Quantidade de horas que o usuário consumiu de Segunda a Quinta.',
        horas_final_de_semana_mensal: 'Quantidade de horas que o usuário consumiu de Sexta a Domingo.',
        horas_madrugada_mensal: 'Quantidade de horas que o usuário consumiu entre 00:00 e 05:59.',
        horas_manha_mensal: 'Quantidade de horas que o usuário consumiu entre 06:00 e 11:59.',
        horas_noite_mensal: 'Quantidade de horas que o usuário consumiu entre 18:00 e 23:59.',
        horas_tarde_mensal: 'Quantidade de horas que o usuário consumiu entre 12:00 e 17:59.',
        diversidade_mensal: 'É a soma de clusters que o usuário consumiu no mês. Exemplo, se o usuário consumiu mais de 1 hr de filmes e novelas, ele terá uma diversidade de 2, mas se ele consumiu menos de 1 hora de filmes e mais de uma hora de novelas, ele terá uma diversidade de 1.',
        diversidade_conteudo_mensal: 'É a soma de todos os programas assistidos pelo usuário no mês. Exemplo: Se o usuário assistiu 2 novelas e 3 filmes, ele terá uma diversidade de conteúdo de 5.',
        perfil_periodo_do_dia_mes: 'Identifica qual o período que o usuário mais consume, sendo de manhã, tarde, noite ou madrugada.',
        perfil_dia_da_semana_mes: 'Identifica se o usuário consome mais em dia da semana ou final de semana.',
        Cluster_Conteudo_Mais_Assistido_Mes: 'Identifica o cluster do conteúdo mais assistido pelo usuário naquele mês. Exemplo: Ao Vivo Tv Globo, Esportes, Filmes, Jornalismo, Novelas, Outros Conteúdos, Reality, Series, Variedades ou Sem Consumo Suficiente (assistiu menos de 1 hora em todos os clusters).'
    }
}

WITH
  base_1 AS (
  SELECT
    globo_id,
    DATE_TRUNC(semana, MONTH) AS mes,
    CASE
      WHEN Cluster_Engajamento = '4. Heavy Users' THEN 'a4_Heavy_Users'
      WHEN Cluster_Engajamento = '3. Mid-Heavy Users' THEN 'a3_Mid_Heavy_Users'
      WHEN Cluster_Engajamento = '2. Mid-Light Users' THEN 'a2_Mid_Light_Users'
      WHEN Cluster_Engajamento = '1. Light Users' THEN 'a1_Light_Users'
  END
    AS Cluster_Engajamento,
    CASE
      WHEN novo_cluster_engajamento = '4. Heavy Users' THEN 'a4_Heavy_Users_novo_cluster_engajamento'
      WHEN novo_cluster_engajamento = '3. Mid-Heavy Users' THEN 'a3_Mid_Heavy_Users_novo_cluster_engajamento'
      WHEN novo_cluster_engajamento = '2. Mid-Light Users' THEN 'a2_Mid_Light_Users_novo_cluster_engajamento'
      WHEN novo_cluster_engajamento = '1. Light Users' THEN 'a1_Light_Users_novo_cluster_engajamento'
  END
    AS novo_cluster_engajamento
  FROM
    ${ref('usuarios', 'RaioXAssinante')} ),
  base_2 AS (
  SELECT
    *
  FROM (
    SELECT
      globo_id,
      mes,
      Cluster_Engajamento
    FROM
      base_1 )
  PIVOT
    ( COUNT (0) FOR Cluster_Engajamento IN ( 'a4_Heavy_Users',
        'a3_Mid_Heavy_Users',
        'a2_Mid_Light_Users',
        'a1_Light_Users' ) ) ),
  base_novo_cluster AS (
  SELECT
    *
  FROM (
    SELECT
      globo_id,
      mes,
      novo_cluster_engajamento
    FROM
      base_1 )
  PIVOT
    ( COUNT (0) FOR novo_cluster_engajamento IN ( 'a4_Heavy_Users_novo_cluster_engajamento',
        'a3_Mid_Heavy_Users_novo_cluster_engajamento',
        'a2_Mid_Light_Users_novo_cluster_engajamento',
        'a1_Light_Users_novo_cluster_engajamento' ) ) ),
  base_3 AS (
  SELECT
    globo_id,
    mes,
    CASE
      WHEN a4_Heavy_Users >= a1_Light_Users AND a4_Heavy_Users >= a3_Mid_Heavy_Users AND a4_Heavy_Users >= a2_Mid_Light_Users THEN '4. Heavy Users'
      WHEN a3_Mid_Heavy_Users >= a1_Light_Users
    AND a3_Mid_Heavy_Users >= a2_Mid_Light_Users
    AND a3_Mid_Heavy_Users >= a4_Heavy_Users THEN '3. Mid-Heavy User'
      WHEN a2_Mid_Light_Users >= a1_Light_Users AND a2_Mid_Light_Users >= a3_Mid_Heavy_Users AND a2_Mid_Light_Users >= a4_Heavy_Users THEN '2. Mid-Light Users'
      WHEN a1_Light_Users >= a2_Mid_Light_Users
    AND a1_Light_Users >= a3_Mid_Heavy_Users
    AND a1_Light_Users >= a4_Heavy_Users THEN '1. Light Users'
  END
    AS Cluster_Engajamento,
    CASE
      WHEN a4_Heavy_Users_novo_cluster_engajamento >= a1_Light_Users_novo_cluster_engajamento AND a4_Heavy_Users_novo_cluster_engajamento >= a3_Mid_Heavy_Users_novo_cluster_engajamento AND a4_Heavy_Users_novo_cluster_engajamento >= a2_Mid_Light_Users_novo_cluster_engajamento THEN '4. Heavy Users'
      WHEN a3_Mid_Heavy_Users_novo_cluster_engajamento >= a1_Light_Users_novo_cluster_engajamento
    AND a3_Mid_Heavy_Users_novo_cluster_engajamento >= a2_Mid_Light_Users_novo_cluster_engajamento
    AND a3_Mid_Heavy_Users_novo_cluster_engajamento >= a4_Heavy_Users_novo_cluster_engajamento THEN '3. Mid-Heavy User'
      WHEN a2_Mid_Light_Users_novo_cluster_engajamento >= a1_Light_Users_novo_cluster_engajamento AND a2_Mid_Light_Users_novo_cluster_engajamento >= a3_Mid_Heavy_Users_novo_cluster_engajamento AND a2_Mid_Light_Users_novo_cluster_engajamento >= a4_Heavy_Users_novo_cluster_engajamento THEN '2. Mid-Light Users'
      WHEN a1_Light_Users_novo_cluster_engajamento >= a2_Mid_Light_Users_novo_cluster_engajamento
    AND a1_Light_Users_novo_cluster_engajamento >= a3_Mid_Heavy_Users_novo_cluster_engajamento
    AND a1_Light_Users_novo_cluster_engajamento >= a4_Heavy_Users_novo_cluster_engajamento THEN '1. Light Users'
  END
    AS novo_cluster_engajamento
  FROM
    base_2
  INNER JOIN
    base_novo_cluster
  USING
    (globo_id,
      mes) ),
  base_4 AS (
  SELECT
    *,
    DATE_TRUNC(semana, MONTH) AS mes
  FROM
    ${ref('usuarios', 'RaioXAssinante')} ),
  base_5 AS (
  SELECT
    globo_id,
    mes,
    assetid,
    data_hora_compra,
    SUM(frequencia) AS frequencia_mensal,
    SUM(Ao_Vivo_Tv_Globo_horas) AS Ao_Vivo_Tv_Globo_horas_Mensal,
    SUM(Ao_Vivo_Tv_Globo_qtnd_programas) AS Ao_Vivo_Tv_Globo_qtnd_programas_Mensal,
    SUM(Ao_Vivo_Tv_Globo_freq) AS Ao_Vivo_Tv_Globo_freq_Mensal,
    SUM(Ao_Vivo_Tv_Globo_qtnd_midias) AS Ao_Vivo_Tv_Globo_qtnd_midias_Mensal,
    SUM(Filmes_horas) AS Filmes_horas_Mensal,
    SUM(Filmes_qtnd_programas) AS Filmes_qtnd_programas_Mensal,
    SUM(Filmes_freq) AS Filmes_freq_Mensal,
    SUM(Filmes_qtnd_midias) AS Filmes_qtnd_midias_Mensal,
    SUM(Series_horas) AS Series_horas_Mensal,
    SUM(Series_qtnd_programas) AS Series_qtnd_programas_Mensal,
    SUM(Series_freq) AS Series_freq_Mensal,
    SUM(Series_qtnd_midias) AS Series_qtnd_midias_Mensal,
    SUM(Novelas_horas) AS Novelas_horas_Mensal,
    SUM(Novelas_qtnd_programas) AS Novelas_qtnd_programas_Mensal,
    SUM(Novelas_freq) AS Novelas_freq_Mensal,
    SUM(Novelas_qtnd_midias) AS Novelas_qtnd_midias_Mensal,
    SUM(Esportes_horas) AS Esportes_horas_Mensal,
    SUM(Esportes_qtnd_programas) AS Esportes_qtnd_programas_Mensal,
    SUM(Esportes_freq) AS Esportes_freq_Mensal,
    SUM(Esportes_qtnd_midias) AS Esportes_qtnd_midias_Mensal,
    SUM(Variedades_horas) AS Variedades_horas_Mensal,
    SUM(Variedades_qtnd_programas) AS Variedades_qtnd_programas_Mensal,
    SUM(Variedades_freq) AS Variedades_freq_Mensal,
    SUM(Variedades_qtnd_midias) AS Variedades_qtnd_midias_Mensal,
    SUM(Jornalismo_horas) AS Jornalismo_horas_Mensal,
    SUM(Jornalismo_qtnd_programas) AS Jornalismo_qtnd_programas_Mensal,
    SUM(Jornalismo_freq) AS Jornalismo_freq_Mensal,
    SUM(Jornalismo_qtnd_midias) AS Jornalismo_qtnd_midias_Mensal,
    SUM(Reality_horas) AS Reality_horas_Mensal,
    SUM(Reality_qtnd_programas) AS Reality_qtnd_programas_Mensal,
    SUM(Reality_freq) AS Reality_freq_Mensal,
    SUM(Reality_qtnd_midias) AS Reality_qtnd_midias_Mensal,
    SUM(Outros_Conteudos_horas) AS Outros_Conteudos_horas_Mensal,
    SUM(Outros_Conteudos_qtnd_programas) AS Outros_Conteudos_qtnd_programas_Mensal,
    SUM(Outros_Conteudos_freq) AS Outros_Conteudos_freq_Mensal,
    SUM(Outros_Conteudos_qtnd_midias) AS Outros_Conteudos_qtnd_midias_Mensal,
    SUM(total_horas) AS total_horas_Mes,
    SUM(horas_dia_de_semana) AS horas_dia_de_semana_mensal,
    SUM(horas_final_de_semana) AS horas_final_de_semana_mensal,
    SUM(horas_madrugada) AS horas_madrugada_mensal,
    SUM(horas_manha) AS horas_manha_mensal,
    SUM(horas_noite) AS horas_noite_mensal,
    SUM(horas_tarde) AS horas_tarde_mensal,
    SUM(Diversidade_Conteudo) AS diversidade_conteudo_mensal
  FROM
    base_4
  GROUP BY
    globo_id,
    mes,
    assetid,
    data_hora_compra ),
  base_6 AS (
  SELECT
    *,
    CASE
      WHEN horas_noite_mensal > horas_madrugada_mensal AND horas_noite_mensal > horas_tarde_mensal AND horas_noite_mensal > horas_manha_mensal THEN 'Noite'
      WHEN horas_tarde_mensal > horas_madrugada_mensal
    AND horas_tarde_mensal > horas_manha_mensal THEN 'Tarde'
      WHEN horas_manha_mensal > horas_madrugada_mensal THEN 'Manhã'
      ELSE 'Madrugada'
  END
    AS perfil_periodo_do_dia_mes,
    CASE
      WHEN horas_dia_de_semana_mensal >= horas_final_de_semana_mensal THEN 'dia_de_semana'
      WHEN horas_dia_de_semana_mensal = horas_final_de_semana_mensal THEN 'igual'
      ELSE 'final_de_semana'
  END
    AS perfil_dia_da_semana_mes,
    CASE
      WHEN Filmes_horas_Mensal < 1 AND Esportes_horas_Mensal < 1 AND Jornalismo_horas_Mensal < 1 AND Reality_horas_Mensal < 1 AND Variedades_horas_Mensal < 1 AND Novelas_horas_Mensal < 1 AND Series_horas_Mensal < 1 AND Outros_Conteudos_horas_Mensal < 1 AND Ao_Vivo_Tv_Globo_horas_Mensal < 1 THEN 'Sem_Consumo_Suficiente'
      WHEN Filmes_horas_Mensal > Novelas_horas_Mensal
    AND Filmes_horas_Mensal > Series_horas_Mensal
    AND Filmes_horas_Mensal > Esportes_horas_Mensal
    AND Filmes_horas_Mensal > Jornalismo_horas_Mensal
    AND Filmes_horas_Mensal > Reality_horas_Mensal
    AND Filmes_horas_Mensal > Variedades_horas_Mensal
    AND Filmes_horas_Mensal > Outros_Conteudos_horas_Mensal
    AND Filmes_horas_Mensal > Ao_Vivo_Tv_Globo_horas_Mensal THEN 'Filmes'
      WHEN Novelas_horas_Mensal > Outros_Conteudos_horas_Mensal AND Novelas_horas_Mensal > Series_horas_Mensal AND Novelas_horas_Mensal > Esportes_horas_Mensal AND Novelas_horas_Mensal > Jornalismo_horas_Mensal AND Novelas_horas_Mensal > Reality_horas_Mensal AND Novelas_horas_Mensal > Variedades_horas_Mensal
    --  AND Novelas_horas > Filmes_horas
    AND Novelas_horas_Mensal > Ao_Vivo_Tv_Globo_horas_Mensal THEN 'Novelas'
      WHEN Series_horas_Mensal > Outros_Conteudos_horas_Mensal
    AND Series_horas_Mensal > Esportes_horas_Mensal
    AND Series_horas_Mensal > Jornalismo_horas_Mensal
    AND Series_horas_Mensal > Reality_horas_Mensal
    AND Series_horas_Mensal > Variedades_horas_Mensal
    AND Series_horas_Mensal > Ao_Vivo_Tv_Globo_horas_Mensal THEN 'Series'
      WHEN Esportes_horas_Mensal > Outros_Conteudos_horas_Mensal AND Esportes_horas_Mensal > Ao_Vivo_Tv_Globo_horas_Mensal THEN 'Esportes'
      WHEN Jornalismo_horas_Mensal > Outros_Conteudos_horas_Mensal
    AND Jornalismo_horas_Mensal > Ao_Vivo_Tv_Globo_horas_Mensal THEN 'Jornalismo'
      WHEN Reality_horas_Mensal > Outros_Conteudos_horas_Mensal AND Reality_horas_Mensal > Ao_Vivo_Tv_Globo_horas_Mensal THEN 'Reality'
      WHEN Variedades_horas_Mensal > Outros_Conteudos_horas_Mensal
    AND Variedades_horas_Mensal > Ao_Vivo_Tv_Globo_horas_Mensal THEN 'Variedades'
      WHEN Ao_Vivo_Tv_Globo_horas_Mensal > Outros_Conteudos_horas_Mensal THEN 'Ao_Vivo_Tv_Globo'
      ELSE 'Outros_Conteudos'
  END
    AS Cluster_Conteudo_Mais_Assistido_Mes
  FROM
    base_5 ),
  base_7 AS (
  SELECT
    *
  FROM
    base_3
  LEFT JOIN
    base_6
  USING
    (globo_id,
      mes) ),
  base_8 AS (
  SELECT
    * EXCEPT(frequencia_mensal,
      Filmes_freq_Mensal,
      Novelas_freq_Mensal,
      Series_freq_Mensal,
      Esportes_freq_mensal,
      Variedades_freq_mensal,
      Jornalismo_freq_mensal,
      Reality_freq_mensal,
      Outros_Conteudos_freq_Mensal,
      Ao_Vivo_Tv_Globo_freq_Mensal),
  IF
    (frequencia_mensal >= 31, 30, frequencia_mensal) AS frequencia_mensal,
  IF
    (Filmes_freq_Mensal >= 31, 30, Filmes_freq_Mensal) AS Filmes_freq_Mensal,
  IF
    (Esportes_freq_mensal >= 31, 30, Esportes_freq_mensal) AS Esportes_freq_mensal,
  IF
    (Variedades_freq_mensal >= 31, 30, Variedades_freq_mensal) AS Variedades_freq_mensal,
  IF
    (Jornalismo_freq_mensal >= 31, 30, Jornalismo_freq_mensal) AS Jornalismo_freq_mensal,
  IF
    (Reality_freq_mensal >= 31, 30, Reality_freq_mensal) AS Reality_freq_mensal,
  IF
    (Novelas_freq_Mensal >= 31, 30, Novelas_freq_Mensal) AS Novelas_freq_Mensal,
  IF
    (Series_freq_Mensal >= 31, 30, Series_freq_Mensal) AS Series_freq_Mensal,
  IF
    (Outros_Conteudos_freq_Mensal >= 31, 30, Outros_Conteudos_freq_Mensal) AS Outros_Conteudos_freq_Mensal,
  IF
    (Ao_Vivo_Tv_Globo_freq_Mensal >= 31, 30, Ao_Vivo_Tv_Globo_freq_Mensal) AS Ao_Vivo_Tv_Globo_freq_Mensal
  FROM
    base_7 ),
  base_9 AS (
  SELECT
    *,
    CASE
      WHEN Ao_Vivo_Tv_Globo_horas_Mensal>0 THEN 1
      ELSE 0
  END
    AS assistiu_ao_vivo_tv_globo,
    CASE
      WHEN Filmes_horas_Mensal>0 THEN 1
      ELSE 0
  END
    AS assistiu_filmes,
    CASE
      WHEN Esportes_horas_Mensal>0 THEN 1
      ELSE 0
  END
    AS assistiu_esportes,
    CASE
      WHEN Variedades_horas_Mensal>0 THEN 1
      ELSE 0
  END
    AS assistiu_variedades,
    CASE
      WHEN Jornalismo_horas_Mensal>0 THEN 1
      ELSE 0
  END
    AS assistiu_jornalismo,
    CASE
      WHEN Reality_horas_Mensal>0 THEN 1
      ELSE 0
  END
    AS assistiu_reality,
    CASE
      WHEN Novelas_horas_Mensal>0 THEN 1
      ELSE 0
  END
    AS assistiu_novelas,
    CASE
      WHEN Series_horas_Mensal>0 THEN 1
      ELSE 0
  END
    AS assistiu_series,
    CASE
      WHEN Outros_Conteudos_horas_Mensal>0 THEN 1
      ELSE 0
  END
    AS assistiu_outros_conteudos
  FROM
    base_8 ),
  base_10 AS (
  SELECT
    *,
    (assistiu_ao_vivo_tv_globo+ assistiu_esportes+ assistiu_variedades+ assistiu_jornalismo+ assistiu_reality+ assistiu_filmes+assistiu_novelas+assistiu_series+assistiu_outros_conteudos) AS diversidade_mensal
  FROM (
    SELECT
      globo_id,
      mes,
      MAX(assistiu_ao_vivo_tv_globo) AS assistiu_ao_vivo_tv_globo,
      MAX(assistiu_filmes) AS assistiu_filmes,
      MAX(assistiu_esportes) AS assistiu_esportes,
      MAX(assistiu_variedades) AS assistiu_variedades,
      MAX(assistiu_jornalismo) AS assistiu_jornalismo,
      MAX(assistiu_reality) AS assistiu_reality,
      MAX(assistiu_novelas) AS assistiu_novelas,
      MAX(assistiu_series) AS assistiu_series,
      MAX(assistiu_outros_conteudos) AS assistiu_outros_conteudos
    FROM
      base_9
    GROUP BY
      1,
      2 ))
SELECT
  globo_id,
  mes,
  Cluster_Engajamento,
  novo_cluster_engajamento,
  assetid,
  data_hora_compra,
  frequencia_mensal,
  Ao_Vivo_Tv_Globo_horas_Mensal,
  Ao_Vivo_Tv_Globo_qtnd_programas_Mensal,
  Ao_Vivo_Tv_Globo_freq_Mensal,
  Ao_Vivo_Tv_Globo_qtnd_midias_Mensal,
  Filmes_horas_Mensal,
  Filmes_qtnd_programas_Mensal,
  Filmes_freq_Mensal,
  Filmes_qtnd_midias_Mensal,
  Series_horas_Mensal,
  Series_qtnd_programas_Mensal,
  Series_freq_Mensal,
  Series_qtnd_midias_Mensal,
  Novelas_horas_Mensal,
  Novelas_qtnd_programas_Mensal,
  Novelas_freq_Mensal,
  Novelas_qtnd_midias_Mensal,
  Esportes_horas_Mensal,
  Esportes_qtnd_programas_Mensal,
  Esportes_freq_Mensal,
  Esportes_qtnd_midias_Mensal,
  Variedades_horas_Mensal,
  Variedades_qtnd_programas_Mensal,
  Variedades_freq_Mensal,
  Variedades_qtnd_midias_Mensal,
  Jornalismo_horas_Mensal,
  Jornalismo_qtnd_programas_Mensal,
  Jornalismo_freq_Mensal,
  Jornalismo_qtnd_midias_Mensal,
  Reality_horas_Mensal,
  Reality_qtnd_programas_Mensal,
  Reality_freq_Mensal,
  Reality_qtnd_midias_Mensal,
  Outros_Conteudos_horas_Mensal,
  Outros_Conteudos_qtnd_programas_Mensal,
  Outros_Conteudos_freq_Mensal,
  Outros_Conteudos_qtnd_midias_Mensal,
  total_horas_Mes,
  horas_dia_de_semana_mensal,
  horas_final_de_semana_mensal,
  horas_madrugada_mensal,
  horas_manha_mensal,
  horas_noite_mensal,
  horas_tarde_mensal,
  diversidade_mensal,
  diversidade_conteudo_mensal,
  perfil_periodo_do_dia_mes,
  perfil_dia_da_semana_mes,
  Cluster_Conteudo_Mais_Assistido_Mes
FROM
  base_8
LEFT JOIN
  base_10
USING
  (globo_id,
    mes)
ORDER BY
  globo_id,
  mes ASC
